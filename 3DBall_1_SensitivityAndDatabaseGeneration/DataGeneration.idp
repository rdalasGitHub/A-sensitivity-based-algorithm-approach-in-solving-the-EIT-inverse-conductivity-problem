load "msh3"
load "tetgen"
load "medit"
include "ffmatlib.idp" //to import to matlab

func real[int] DataGeneration(real r, real d, real[int] proj, real[int] projdirec) {

//Parameters
int boundaryLabel=1;
    //center-radius of perturbation
	real[int] center(3);
	center = proj + d*projdirec;
	real px = center[0];
	real py = center[1];
	real pz = center[2];
	real pr = r;
//mesh
mesh3 ball = readmesh3("ball.mesh");

//Definition of the Current Density on the Boundary
func f0s=sin(x*y);
real f1s = int2d(ball,boundaryLabel)(f0s);
real f2s = int2d(ball,boundaryLabel)(1);
func nbf= f0s*f2s - f1s;
real f3s = int2d(ball,boundaryLabel)(nbf);

//finite element space
fespace Vh(ball,P1);
fespace Ph(ball,P0);
//defining sigma in fespace
Ph<real> sigdata;
Ph<real> reg;

//sigma value of the inclusions
real sig1 = 0.33;
sigdata = sig1;
{
	//regions for sigma
	reg = ((x-px)*(x-px)+(y-py)*(y-py)+(z-pz)*(z-pz) <= pr*pr); //sphere
}

/////////////////////////////////////////START 3D Forward Problem/////////////////////////////////////////
//The Matrices
Vh uh,vh;

int nna=Vh.ndof; //number of nodes 
int n1=nna+1; //number of unknowns
macro Grad(u) [dx(u),dy(u),dz(u)]//

//submatrix A
varf m1(uh,vh) = int3d(ball) (sigdata*Grad(uh)'*Grad(vh)); 
matrix M1=m1(Vh,Vh);
//submatrix b
varf m2(uh,vh) = int3d(ball)(vh);
real[int] M2=m2(0,Vh);
//The Matrix
matrix Kdata=[[M1,M2],[M2',0]];

//RHS of the equation
Vh ut=nbf;
varf vr(uh,vh)=int2d(ball,boundaryLabel)(ut*vh);
real[int] bb(n1),bb1(1),b(nna);
bb1=0.;
b=vr(0,Vh);
bb=[b,bb1]; 

//Solving the Forward System
real[int] nu(n1), c(1), u0(nna);
set(Kdata,solver=UMFPACK);
nu=Kdata^-1*bb;
[u0,c]=nu;
/////////////////////////////////////////END 3D Forward Problem/////////////////////////////////////////

/////////////////////////////////////////START Sensitivity/////////////////////////////////////////
//perturbation characteristics
Ph mu; //direction of perturbations (charteristic function)
mu = reg;

//Constructing the RHS of the Sensitivity
varf vrs(uh,vh)= int3d(ball)(-mu * Grad(uh)' * Grad(vh));
matrix ms = vrs(Vh,Vh);

real[int] MS(nna), bb1s(1), bbs(n1);
MS = ms*u0;
bb1s = 0.;
bbs = [MS, bb1s];

//Solving the Sensitivity
real[int] nus(n1), cs(1);
Vh u0s;
set(Kdata, solver=UMFPACK);
nus=Kdata^-1*bbs;
[u0s[],cs]=nus; //u0s is the sensitivity of potential. cs penalty term coefficient
/////////////////////////////////////////END Sensitivity/////////////////////////////////////////
//ffSaveData(u0s,"sensitivity_122.txt");

//getting indices of u0s belonging on the boundary
int NbBoundaryElements = ball.nbe;
real[int] UUU(3*NbBoundaryElements), xx(3*NbBoundaryElements), yy(3*NbBoundaryElements), zz(3*NbBoundaryElements); 
for (int k = 0; k < NbBoundaryElements; k++){
//indices of u0s on the boundary    
int Vertex1 = ball.be(k)[0];
int Vertex2 = ball.be(k)[1];
int Vertex3 = ball.be(k)[2];
UUU[k] = Vertex1;
UUU[k+NbBoundaryElements] = Vertex2;
UUU[k+2*NbBoundaryElements] = Vertex3;
}

//Getting unique boundary indices. 
//UUUunique contains unique indices
UUU.sort;
real[int] UUUuniquetemp(UUU.n);
int numberofboundaryindices=1;
UUUuniquetemp[0]=UUU[0];
for(int k=0; k<UUU.n-1; k++){
    if(UUU[k]!=UUU[k+1]){
        UUUuniquetemp[numberofboundaryindices]=UUU[k+1];
        numberofboundaryindices++;
    }
}
real[int] UUUunique(numberofboundaryindices);
UUUunique = UUUuniquetemp; //getting rid of zeros

//gettting max value of the sensitivity on the boundary
//u1borderposunique contains absolute values of the sensitivity on the boundary
int tempindexofu1;
real[int] u1borderposunique(UUUunique.n);
for (int k=0; k<UUUunique.n ;k++){
    tempindexofu1 = UUUunique[k];
    u1borderposunique[k]=abs(u0s[][tempindexofu1]);
}

//counting the "affected" indices on the boundary
//u1ofaffected contains the sensitivity values of the "affected" part of the boundary
real numberofaffected = 0.;
real tol=0.2;
real[int] u1ofaffected(UUUunique.n);
for (int k=0; k<UUUunique.n ;k++){
    if (u1borderposunique[k] > tol*u1borderposunique.max){
        u1ofaffected[numberofaffected] = u1borderposunique[k];
        numberofaffected = numberofaffected + 1.;
    }
}
//to avoid division by 0
if (numberofaffected == 0.){
    numberofaffected = 1.;
}
real normofaffected= sqrt(u1ofaffected'*u1ofaffected);
real normofboundary = sqrt(u1borderposunique'*u1borderposunique); 
real R2LHS = normofaffected/numberofaffected;
real R3CubeRootofNormofBoundary = normofboundary^(1./3.);

real[int] datagenerated = [R2LHS, R3CubeRootofNormofBoundary];
return datagenerated;
}